\section{Quicksort}

Der Quicksort ist ein rekursiver Algorithmus zum Sortieren einer Liste $L$ im Indexbereich $a:e$. Erfunden wurde dieser von \person{Sir Charles Antony Richard Hoare}. Der Algorithmus läuft wie folgt ab:
\begin{enumerate}
	\item Wähle beliebiges Element aus $L$, dieses habe den key $W$ (sogenanntes \begriff{Pivotelement}). Ideal wäre, wenn $W$ der Median aller keys wäre. Der schlechteste Fall wäre, wenn $W$ ein Extremum wäre.
	\item Bilde Partition $L_L\mid L_R$ der Liste $L$ mit:
	\begin{itemize}
		\item alle Elemente von $L_L$ haben keys $\le W$
		\item alle Elemente von $L_R$ haben keys $\ge W$
	\end{itemize}
	\item Sortieren der beiden Listen mittels rekursivem Aufruf von Quicksort
\end{enumerate}

Als Quellcode sieht Quicksort dann so aus:
\begin{lstlisting}
recursive subroutine QsortC(A)
 real, intent(in out), dimension(:) :: A
 integer :: iq

 if(size(A) > 1) then
  call Partition(A, iq)
  call QsortC(A(:iq-1))
  call QsortC(A(iq:))
 endif
end subroutine QsortC

subroutine Partition(A, marker)
 real, intent(inout), dimension(:) :: A
 integer, intent(out) :: marker
 integer :: i, j
 real :: temp
 real :: x      ! pivot point

 x = A(1)
 i= 0
 j= size(A) + 1

 do
  j = j-1
  do
   if (A(j) <= x) exit
   j = j-1
  end do
  
  i = i+1
  
  do
   if (A(i) >= x) exit
   i = i+1
  end do
  
  if (i < j) then ! exchange A(i) and A(j)
   temp = A(i)
   A(i) = A(j)
   A(j) = temp
  elseif (i == j) then
   marker = i+1
   return
  else
   marker = i
   return
  endif
 end do
end subroutine Partition
\end{lstlisting}

Im worst case, das heißt das Pivotelement ist ein Extremum, wird immer nur 1 Element abgespalten. Dann hat Quicksort die Komplexität $T(n)=\mathcal{O}(n^2)$. Im average case gilt: $T(n)=\mathcal{O}(n\log_2 n)$ und im best case hat Quicksort die Komplexität $T(n)=\Omega(n\log_2 n)$.

Zuletzt noch ein Blick auf die Eigenschaften:
\begin{itemize}
	\item in situ
	\item nicht stabil
	\item hat nur sehr einfache Operationen
	\item für große $n$ sehr schnell
	\item pro Durchlauf $\mathcal{O}(n)$
	\item für kleine $n$ eher schlecht
\end{itemize}